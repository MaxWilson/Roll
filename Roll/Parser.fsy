%{


%}

// The start token becomes a parser function in the compiled code:
%start start

// Misc tokens
%token EOF

%token <int> Number
%token DSymbol
%token Plus
%token Minus
%token Dot
%token Min
%token Max
%token Advantage
%token Disadvantage
%token Comma
%token OpenParen
%token CloseParen
%token QuestionMark
%token Quit
%token Set
%token Print
%token Add
%token <string> Ident

// This is the type of the data produced by a successful reduction of the 'start'
// symbol:
%type < Statements.Command > start

%%

// These are the rules of the grammar along with the F# code of the 
// actions executed as rules are reduced.  
start: Command EOF { $1 }

Command:
	| MultiRoll { Statements.RollCommand($1) }
	| Quit { Statements.QuitCommand }
	| SetValue { $1 }
	| PrintValue { $1 }

SetValue:
	| Set Ident { Statements.SetContext $2 }
	| Set Ident Ident Idents { Statements.SetValue(Some $2, $3, $4) }
	//| Set Ident Idents { Statements.SetValue(None, $2, $3) }
	| Set Ident Number { Statements.SetValue(None, $2, $3.ToString()) }
	| Set Ident Ident Number { Statements.SetValue(Some $2, $3, $4.ToString()) }
	| Add Ident Ident Number { Statements.AddValue(Some $2, $3, $4) }
	| Add Ident Number { Statements.AddValue(None, $2, $3) }

Idents:
	| Ident				{ $1 }
	| Ident Idents		{ $1 + " " + $2 }

PrintValue:
	| Print Ident { Statements.PrintValues (Some $2) }
	| Print { Statements.PrintValues None }

MultiRoll:
	| Number Dot MaybeTest { Statements.Repeat($1, $3) }
	| MaybeTest { $1 }

MaybeTest:
	| Roll { Statements.Simple $1 }
	| Roll QuestionMark Number { Statements.AtLeast(Statements.Simple ($1), $3, 20) }
	| Roll QuestionMark Number OpenParen Number CloseParen { Statements.AtLeast(Statements.Simple ($1), $3, $5) }
	
Roll:
	| PlusList { $1 }
	| Min RollList { Statements.Min($2) }
	| Max RollList { Statements.Max($2) }
	
RollList:
	| SimpleRoll Comma RollList { List.append [$1] $3 }
	| SimpleRoll { [$1] }

PlusList:
	| SimpleRoll { $1 }
	| SimpleRoll Plus PlusList { Statements.MakeSum($1, $3) }
	| SimpleRoll Minus PlusList { Statements.MakeSubtract($1, $3) }

SimpleRoll:
	| Number						{ Statements.Roll(0, 0, $1) }
	| Number DSymbol				{ Statements.Roll($1, 6, 0) }
	| Number DSymbol Number			{ Statements.Roll($1, $3, 0) }
	| DSymbol Number 			    { Statements.Roll(1, $2, 0) }
	| SimpleRoll Advantage					{ Statements.Max [$1; $1] }
	| SimpleRoll Disadvantage				{ Statements.Min [$1; $1] }

